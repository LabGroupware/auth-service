plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'org.cresplanex.account'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://buf.build/gen/maven"
	}
}

ext {
	set('jwtVersion', '0.12.6')
	set('logstashEncoder', "8.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation "net.logstash.logback:logstash-logback-encoder:${logstashEncoder}"
	implementation 'org.flywaydb:flyway-core'
	implementation "org.flywaydb:flyway-database-postgresql"
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.session:spring-session-data-redis'
	implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
	implementation "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"
	implementation 'com.google.code.findbugs:jsr305:3.0.2'

	implementation "org.cresplanex.api.state:common:1.0.6"

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

import java.text.SimpleDateFormat

task generateMigrationFile {
	def migrationName = project.hasProperty('migrationName') ? project.property('migrationName') : 'create_table'

	def dir = project.hasProperty('dir') ? project.property('dir') : 'ddl'

	doLast {
		// 日付フォーマットを使用して、ファイル名に現在の日時を追加
		def timestamp = new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
		def fileName = "V${timestamp}__${migrationName}.sql"
		// ディレクトリを作成して、ファイルを生成
		def migrationDir = file("src/main/resources/db/migration/${dir}")
		migrationDir.mkdirs()

		// ファイルを作成
		def file = new File(migrationDir, fileName)
		file.createNewFile()
		println "Migration file created at: ${file.path}"
	}
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
	filesMatching('**/application.yaml') {
		filter(
				ReplaceTokens,
				tokens: [
						'applicationName': "${rootProject.name}".toString(),
						'version': "${version}".toString(),
				]
		)
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

ext {
	imageVersion = project.hasProperty('imageVersion') ? project.imageVersion : 'latest'
	imageArchitecture = project.hasProperty('imageArchitecture') ? project.imageArchitecture : 'amd64'
	imageOs = project.hasProperty('imageOs') ? project.imageOs : 'linux'
}

jib {
	from {
		platforms {
			platform {
				architecture = imageArchitecture
				os = imageOs
			}
		}
	}
	to {
		image = "ablankz/nova-auth-service"
		tags = ["${imageVersion}-${imageArchitecture}", "latest-${imageArchitecture}"]
	}
}

def platforms = [
		['os': 'linux', 'arch': 'amd64'],
		['os': 'linux', 'arch': 'arm64']
]

import com.google.cloud.tools.jib.gradle.PlatformParameters

platforms.each { platform ->

	def taskName = "jibDockerBuild_${platform.os.capitalize()}${platform.arch.capitalize()}"

	tasks.register(taskName) {
		group = 'docker'
		description = "Build Docker image for ${platform.os}/${platform.arch}"
		PlatformParameters pltParam = new PlatformParameters();
		pltParam.setArchitecture(platform.arch)
		pltParam.setOs(platform.os)

		doLast {
			jib.from.platforms.unset()
			jib.from.platforms.addAll([pltParam])
			jib.to.image = "ablankz/nova-auth-service"
			jib.to.tags = ["${imageVersion}-${platform.arch}", "latest-${platform.arch}"]
			jibDockerBuild.buildDocker()
		}
	}
}

tasks.register('jibMultiBuild') {
	group = 'docker'
	dependsOn tasks.matching { it.name.startsWith('jibDockerBuild_') }
}
